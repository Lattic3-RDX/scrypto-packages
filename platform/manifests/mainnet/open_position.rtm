# ------------------- User ------------------- #

# Take own 0.2xUSDC
CALL_METHOD
    Address("account_rdx12y4jmlyavnm3fkdc2ckuxpslnvt07cy8txmnyglve855q7m0akjtqp")
    "withdraw"
    Address("resource_rdx1t4upr78guuapv5ept7d7ptekk9mqhy605zgms33mcszen8l9fac8vf")
    Decimal("0.2")
;

# ------------------- Weft ------------------- #

# Take flash loan for 0.4xUSDC
CALL_METHOD
    Address("component_rdx1czmr02yl4da709ceftnm9dnmag7rthu0tu78wmtsn5us9j02d9d0xn")
    "take_flash_loan"
    Map<Address, Decimal>(
        Address("resource_rdx1t4upr78guuapv5ept7d7ptekk9mqhy605zgms33mcszen8l9fac8vf") => Decimal("0.2")
    )
;

# Collect all xUSDC into a bucket
TAKE_ALL_FROM_WORKTOP
    Address("resource_rdx1t4upr78guuapv5ept7d7ptekk9mqhy605zgms33mcszen8l9fac8vf")
    Bucket("supply")
;

# Take 0XRD for no reason
CALL_METHOD
    Address("account_rdx12y4jmlyavnm3fkdc2ckuxpslnvt07cy8txmnyglve855q7m0akjtqp")
    "withdraw"
    Address("resource_rdx1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxradxrd")
    Decimal("0")
;
TAKE_FROM_WORKTOP
    Address("resource_rdx1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxradxrd")
    Decimal("0")
    Bucket("bucket1")
;

# Create position with 0.6xUSDC supply
CALL_METHOD
    Address("component_rdx1cpy6putj5p7937clqgcgutza7k53zpha039n9u5hkk0ahh4stdmq4w")
    "create_cdp"
    Bucket("bucket1")
    Enum<1u8>(
        "lattic3_test_1"
    )
    Enum<0u8>()
    Enum<0u8>()
    Array<Bucket>(
        Bucket("supply")
    )
    Array<Bucket>()
    Map<Address, Decimal>()
;

# Create CDP Bucket
TAKE_NON_FUNGIBLES_FROM_WORKTOP
    Address("resource_rdx1nt22yfvhuuhxww7jnnml5ec3yt5pkxh0qlghm6f0hz46z2wfk80s9r")
    Array<NonFungibleLocalId>(
        NonFungibleLocalId("#218#"),
    )
    Bucket("cdp")
;

# Make proof from CDP
CREATE_PROOF_FROM_BUCKET_OF_ALL
    Bucket("cdp")
    Proof("Proof1")
;

# Borrow 0.4xUSDC in xwBTC 
CALL_METHOD
    Address("component_rdx1cpy6putj5p7937clqgcgutza7k53zpha039n9u5hkk0ahh4stdmq4w")
    "cdp_batch_operation"
    Proof("Proof1")
    Array<Bucket>()
    Array<Bucket>()
    Map<Address, Decimal>(
        Address("resource_rdx1t580qxc7upat7lww4l2c4jckacafjeudxj5wpjrrct0p3e82sq4y75") => Decimal("0.0000033")
    )
    Array<Bucket>()
    Map<Address, Decimal>()
    Map<Address, Array>()
;

# ------------------ OciSwap ----------------- #

# Swap xwBTC to XRD
TAKE_ALL_FROM_WORKTOP
    Address("resource_rdx1t580qxc7upat7lww4l2c4jckacafjeudxj5wpjrrct0p3e82sq4y75")
    Bucket("input_bucket_0")
;
CALL_METHOD
    Address("component_rdx1cpcd3wxrwgtldnrtkz995dzj6zhsqg2sym9tpqgmg7g9wsuu00jn65")
    "swap"
    Bucket("input_bucket_0")
;

# Swap XRD to xUSDC
TAKE_ALL_FROM_WORKTOP
    Address("resource_rdx1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxradxrd")
    Bucket("input_bucket_1")
;
CALL_METHOD
    Address("component_rdx1cq76gg42k73e9y7fcpy2q6d83nr703fa3xztc7lz5dnw38xkp2l5pt")
    "swap"
    Bucket("input_bucket_1")
;
                    
ASSERT_WORKTOP_CONTAINS
    Address("resource_rdx1t4upr78guuapv5ept7d7ptekk9mqhy605zgms33mcszen8l9fac8vf")
    Decimal("0.3")
;

# ------------------- Weft ------------------- #

# Collect all swapped xUSDC
TAKE_ALL_FROM_WORKTOP
    Address("resource_rdx1t4upr78guuapv5ept7d7ptekk9mqhy605zgms33mcszen8l9fac8vf")
    Bucket("loan_repayment")
;

# Take loan terms
TAKE_ALL_FROM_WORKTOP
    Address("resource_rdx1nfeyk5cmjep98zae9vhg067elm2necwsvdnngp3je62ahqstsa8x2x")
    Bucket("loan_terms")
;

# Repay flash loan
CALL_METHOD
    Address("component_rdx1czmr02yl4da709ceftnm9dnmag7rthu0tu78wmtsn5us9j02d9d0xn")
    "repay_flash_loan"
    Array<Bucket>(
        Bucket("loan_repayment")
    )
    Bucket("loan_terms")
;

# ------------------- User ------------------- #

# Deposit CDP into account
CALL_METHOD
    Address("account_rdx12y4jmlyavnm3fkdc2ckuxpslnvt07cy8txmnyglve855q7m0akjtqp")
    "deposit"
    Bucket("cdp")
;

CALL_METHOD
    Address("account_rdx12y4jmlyavnm3fkdc2ckuxpslnvt07cy8txmnyglve855q7m0akjtqp")
    "deposit_batch"
    Expression("ENTIRE_WORKTOP")
;